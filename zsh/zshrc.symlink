#!/bin/sh
export OS_NAME=`uname`
# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles
export DOCUMENTS_PATH=~/Documents
# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)
# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -Uz compinit
if [[ -n ${ZDOTDIR}/.zcompdump(#qN.mh+24) ]]; then
	compinit;
else
	compinit -C;
fi;

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

if [[ ($OS_NAME == 'Linux') && (-d "$HOME/.asdf") ]]; then
  source $HOME/.asdf/asdf.sh
fi

if [[ ($OS_NAME == 'Darwin') ]]; then
  source $(brew --prefix asdf)/libexec/asdf.sh
fi

# Register ssh key
if [[ ($OS_NAME == 'Darwin') ]] then
  ssh-add -K &> /dev/null
else
  ssh-add -l &> /dev/null
fi

# GPG git
export GPG_TTY=$(tty)

unsetopt PROMPT_SP

source $ZSH/antigen.zsh

antigen bundle zsh-users/zsh-autosuggestions
# antigen bundle sobolevn/wakatime-zsh-plugin
antigen bundle chrisands/zsh-yarn-completions

antigen apply

if [[ -a ~/.docker/init-zsh.sh ]]
then
  source ~/.docker/init-zsh.sh || true # Added by Docker Desktop
fi
